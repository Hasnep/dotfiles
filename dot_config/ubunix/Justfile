#!/usr/bin/env just --justfile

xdg_data_home := env_var("XDG_DATA_HOME")
xdg_config_home := env_var("XDG_CONFIG_HOME")
home := env_var("HOME")

fonts_folder := xdg_data_home / "fonts"
fish_config_folder := xdg_config_home / "fish"

ubunix_config_folder := xdg_config_home / "ubunix"
packages_file := ubunix_config_folder / "packages.yaml"

guix_config_folder := xdg_config_home / "guix"
guix_manifest_file := guix_config_folder / "manifest.scm"
guix_data_folder := xdg_data_home / "guix"
guix_profile_folder := guix_data_folder / "default"
guix_activate_script := guix_config_folder / "activate-guix.fish"

apt_manifest_file := ubunix_config_folder / "apt-manifest.fish"

nix_manifest_folder := xdg_config_home / "home-manager"
nix_config_folder := xdg_config_home / "nix"
# nix_profile_folder := xdg_data_home / "nix"
nix_profile_folder := home / ".nix-profile"
nix_bin_folder := nix_profile_folder / "bin"

flatpak_manifest_file := ubunix_config_folder / "flatpak-manifest.fish"

default: generate flatpak guix apt nix

sort_packages:
    cat {{packages_file}} | yq . | sort-ubunix-packages | sponge {{packages_file}} \
        && prettier --write {{packages_file}}

generate:
    cat {{packages_file}} | yq . > /tmp/ubunix-packages.json
    ubunix \
        --packages-file=/tmp/ubunix-packages.json \
        --guix-manifest-file={{guix_manifest_file}} \
        --apt-manifest-file={{apt_manifest_file}} \
        --nix-manifest-folder={{nix_manifest_folder}} \
        --flatpak-manifest-file={{flatpak_manifest_file}} \
        --guix-profile-path={{guix_profile_folder}} \
        --nix-profile-path={{nix_profile_folder}}

flatpak: install_flatpak

guix: update_activate_guix install_guix

apt: install_apt

nix: install_nix install_fonts_nix

install_flatpak:
    fish {{flatpak_manifest_file}}

update_activate_guix:
    #!/usr/bin/env fish
    echo -n '' > {{guix_activate_script}}
    guix package --search-paths -p {{guix_data_folder}}/current 2>/dev/null | bash-exports-to-fish >> {{guix_activate_script}}
    echo '    set --export GUIX_MANIFEST {{guix_manifest_file}}' >> {{guix_activate_script}}
    echo '    set --export GUIX_PROFILE {{guix_profile_folder}}' >> {{guix_activate_script}}
    guix package --search-paths --profile={{guix_profile_folder}} 2>/dev/null | bash-exports-to-fish >> {{guix_activate_script}}
    echo '    set --export GUIX_LOCPATH {{guix_profile_folder}}/lib/locale' >> {{guix_activate_script}}
    fish_indent --write {{guix_activate_script}}
    echo "Updated '{{guix_activate_script}}'."

install_guix:
    mkdir -p {{guix_data_folder}}
    guix package --manifest={{guix_manifest_file}} --profile={{guix_profile_folder}}

install_fonts_guix:
    rm -rf {{fonts_folder}}/guix \
        && cp -r --no-preserve=mode,ownership {{guix_profile_folder}}/share/fonts {{fonts_folder}}/guix

install_apt:
    fish {{apt_manifest_file}}

install_nix:
    if type home-manager >/dev/null 2>&1; then \
        home-manager switch; \
    else \
        nix run home-manager/master -- switch; \
    fi

install_fonts_nix:
    mkdir -p {{nix_profile_folder}}/share/fonts \
        && rm -rf {{fonts_folder}}/nix \
        && mkdir -p {{fonts_folder}}/nix \
        && cp -r --no-preserve=mode,ownership --dereference {{nix_profile_folder}}/share/fonts {{fonts_folder}}/nix
